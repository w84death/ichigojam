' CAT ON THE RUN / Version 1.0 // 1024 bytes
' Krzysztof Krystian Jankowski / https://ichigojam.p1x.in
' Copy from 10-730
10 VIDEO3:CLS:SRND 7:WAIT20:PLAY"E4C8CD4D+8D8"
20 LC 5,3:?"~P1X~";:WAIT100:DRAW 2,12,29,12:DRAW 2,17,29,17
30 LC 1,7:?"CAT ON THE RUN  V1.0";:WAIT120:VIDEO4
100 M=15:N=11:U=7:V=7:C=0:S=0:E=3000
110 FOR Y=0TO N:LC 0,0
120 FOR X=0TO M:?CHR$(RND(4));:NEXT
130 IF Y<N-1 SCROLL2:NEXT
140 LC 4,9:?"PUSH ‚Üê,‚Üí";:CLT
200 LC U,V:?"„Éª";
210 IF BTN(LEFT)GOTO400
220 IF BTN(RIGHT)GOTO500
230 IF BTN(UP) OR C>8GOTO300
240 IF TICK()>E GOTO700
250 LC U,V:?"üê±";:C=C+1
260 LC 0,11:?"üí∞";:?S;:?" üåÄ";:?E-TICK();:?" ";
270 GOTO200
300 IF SCR(U,V-1)=5 GOTO700
310 IF SCR(U,V-1)=1 BEEP200,4:GOTO250:ELSE:
320 SCROLL2:LC 0,0:FOR X=0TO M-1:?CHR$(RND(6));:NEXT
330 S=S+1:C=0:BEEP40,2:GOTO250
400 IF SCR(U-1,V)=5 GOTO700
410 IF SCR(U-1,V)=1 BEEP200,4:GOTO250:ELSE:
420 SCROLL1:FOR Y=0TO N-1:LC 0,Y:?CHR$(RND(6));:NEXT
430 GOTO250
500 IF SCR(U+1,V)=5 GOTO700
510 IF SCR(U+1,V)=1 BEEP200,4:GOTO250:ELSE:
520 SCROLL3:FOR Y=0TO N-1:LC M,Y:?CHR$(RND(6));:NEXT
530 GOTO250
700 VIDEO3:CLS:PLAY"CFEDCCC"
710 LC 3,V:?"GAME OVER";
720 LC 3,V+2:?"SCORE:";:?S
730 WAIT300:GOTO10